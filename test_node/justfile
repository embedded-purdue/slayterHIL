# ------------------
#  Configurables
# ------------------
SIM_BOARD        := "qemu_x86"

ESP_BOARD        := "esp32s3_devkitc/esp32s3/procpu"
ESP_PORT         := "/dev/tty.usbserial-110"          # adjust

CMAKE_CACHE_ARGS := "-- -DCMAKE_EXPORT_COMPILE_COMMANDS=ON"

# set shell := ["bash", "-eu", "-o", "pipefail", "-c"]

# ------------------
#  Helpers
# ------------------
_verify_workspace:
  test -d .west || { echo "ERROR: .west not found here. Ensure in firmware/ and README installation instructions have been followed."; exit 1; }

# ------------------
#  Sim (QEMU)
# ------------------

build-sim: _verify_workspace clean
  west build \
    -p always \
    -b {{SIM_BOARD}} \
    -d build \
    app \
    {{CMAKE_CACHE_ARGS}}

run-sim: build-sim
  west build -t run -d build

# ------------------
#  ESP32-S3
# ------------------

build-esp32: _verify_workspace clean
  west build \
    -p always \
    -b "{{ESP_BOARD}}" \
    -d build \
    app \
    {{CMAKE_CACHE_ARGS}} \
    -DEXTRA_DTC_OVERLAY_FILE="{{justfile_directory()}}/app/boards/esp32s3_devkitc.overlay"

# build
build-esp32-spi-bitbang: clean
  west build \
    -p always \
    -b "{{ESP_BOARD}}" \
    -d build \
    zephyr/samples/drivers/spi_bitbang \
    {{CMAKE_CACHE_ARGS}} \
    -DEXTRA_DTC_OVERLAY_FILE="{{justfile_directory()}}/app/boards/esp32s3_devkitc_spi_bitbang.overlay"

build-esp32-spi-test: _verify_workspace clean
  west build \
    -p always \
    -b "{{ESP_BOARD}}" \
    -d build \
    app \
    {{CMAKE_CACHE_ARGS}} \
    -DEXTRA_DTC_OVERLAY_FILE="{{justfile_directory()}}/app/boards/myspi.overlay" \
    -DCONFIG_COMPILER_TRACK_MACRO_EXPANSION=n

flash-esp32:
  west flash --build-dir build

monitor-esp32:
  python -m serial.tools.miniterm "{{ESP_PORT}}" 115200

run-esp32: build-esp32 flash-esp32 monitor-esp32

run-esp32-spi-test: build-esp32-spi-test flash-esp32 monitor-esp32


# ------------------
#  Defaults
# ------------------

clean:
  rm -rf build
