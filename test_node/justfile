# ------------------
#  Configurables
# ------------------
SIM_BOARD        := "qemu_x86"

ESP_BOARD        := "esp32s3_devkitc/esp32s3/procpu"
ESP_PORT         := "/dev/tty.USB0"          # adjust

NUCLEO_BOARD     := "nucleo_h563zi" #? got it from zephyr/boards/st/nucleo_h563zi/nucleo_h563zi.yaml

CMAKE_CACHE_ARGS := "-- -DCMAKE_EXPORT_COMPILE_COMMANDS=ON"
SAMPLE_DIR := "sample_apps"

# set shell := ["bash", "-eu", "-o", "pipefail", "-c"]

# ------------------
#  Helpers
# ------------------
_verify_workspace:
  test -d .west || { echo "ERROR: .west not found here. Ensure in firmware/ and README installation instructions have been followed."; exit 1; }

# ------------------
#  Sim (QEMU)
# ------------------

build-sim: _verify_workspace clean
  west build \
    -p always \
    -b {{SIM_BOARD}} \
    -d build \
    app \
    {{CMAKE_CACHE_ARGS}}

run-sim: build-sim
  west build -t run -d build

# ------------------
#  NUCLEO H563ZI
# ------------------

build-nucleo: _verify_workspace clean
  west build \
    -p always \
    -b "{{NUCLEO_BOARD}}" \
    -d build \
    app \
    {{CMAKE_CACHE_ARGS}}
  #* NOT ADDING OVERLAY BECAUSE .DTSI FILE "zephyr/boards/st/nucleo_h563zi/nucleo_h563zi-common.dtsi" ALREADY ENABLES SPI & I2C    
# ------------------
#  ESP32-S3
# ------------------

build-esp32: _verify_workspace clean
  west build \
    -p always \
    -b "{{ESP_BOARD}}" \
    -d build \
    app \
    {{CMAKE_CACHE_ARGS}} \
    -DEXTRA_DTC_OVERLAY_FILE="{{justfile_directory()}}/app/boards/esp32s3_devkitc.overlay"

build-esp32-spi-example: _verify_workspace clean
  west build \
    -p always \
    -b "{{ESP_BOARD}}" \
    -d build \
    -s {{SAMPLE_DIR}}/spi_example_app \
    {{CMAKE_CACHE_ARGS}} \
    -DEXTRA_DTC_OVERLAY_FILE="{{justfile_directory()}}/{{SAMPLE_DIR}}/spi_example_app/boards/spi_esp32s3_devkitc.overlay"

build-esp32-spi-bitbang: _verify_workspace clean
  west build \
    -p always \
    -b "{{ESP_BOARD}}" \
    -d build \
    -s {{SAMPLE_DIR}}/spi_bitbang_app \
    {{CMAKE_CACHE_ARGS}} \
    -DEXTRA_DTC_OVERLAY_FILE="{{justfile_directory()}}/{{SAMPLE_DIR}}/spi_bitbang_app/boards/spi_esp32s3_devkitc.overlay"

build-esp32-ipm-esp32: _verify_workspace clean
  west build \
    -p always \
    -b esp32s3_devkitc/esp32s3/procpu \
    -d build \
    -s {{SAMPLE_DIR}}/ipm_esp32  --sysbuild

flash-esp32:
  west flash --build-dir build

monitor-esp32:
  python -m serial.tools.miniterm "{{ESP_PORT}}" 115200

run-esp32: build-esp32 flash-esp32 monitor-esp32

run-esp32-spi-example: build-esp32-spi-example flash-esp32 monitor-esp32

run-esp32-spi-bitbang: build-esp32-spi-bitbang flash-esp32 monitor-esp32

run-esp32-ipm-esp32: build-esp32-ipm-esp32 flash-esp32 monitor-esp32

attach-wsl-usb-port:
  #!/usr/bin/env bash
  set -euo pipefail

  BUSID=$(usbipd.exe list | grep "CP2102N USB to UART Bridge Controller" | awk 'NR==1 {print $1}')

  if [ -z "$BUSID" ]; then
    echo "Device not found: CP2102N USB to UART Bridge Controller"
    echo "This error may be because you are using a USB adapter."
    exit 1
  fi

  echo "Found device on BUSID: $BUSID"

  usbipd.exe bind --busid "$BUSID"
  usbipd.exe attach --wsl --busid "$BUSID"

  echo "Device attached to WSL."


# ------------------
#  Defaults
# ------------------

clean:
  rm -rf build
